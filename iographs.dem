set datafile separator ","
set xlabel "Timestamp (seconds)"
set xtics 60

set xrange [1211881195:1211886260]

# To convert .EPS to .PNG: find tmp -name '*.eps' -exec convert -density 800x600 {} {}.png \;

# LBA access

set title "I/O access - disk seeks" 
set ylabel "LBA (offset)"
set ytics 104857600

set term postscript eps enhanced color size 20cm,15cm
set output 'tmp/graph_seeks.eps'

plot	"<awk 'BEGIN {FS=\",\"} { iosize=$7*$8/1024 \; if (iosize > 0   && iosize <= 32  ) print $1\",\"$6 }' data/dump-simul.csv" using 1:2 with points lt rgb "#6c971e" pt 7 ps 0.2  title 'size < 32KiB', \
	"<awk 'BEGIN {FS=\",\"} { iosize=$7*$8/1024 \; if (iosize > 32  && iosize <= 128 ) print $1\",\"$6 }' data/dump-simul.csv" using 1:2 with points lt rgb "#6c971e" pt 7 ps 0.3  title 'size < 128KiB', \
	"<awk 'BEGIN {FS=\",\"} { iosize=$7*$8/1024 \; if (iosize > 128 && iosize <= 1024) print $1\",\"$6 }' data/dump-simul.csv" using 1:2 with points lt rgb "#2c971e" pt 7 ps 0.4  title 'size < 1024KiB', \
	"<awk 'BEGIN {FS=\",\"} { iosize=$7*$8/1024 \; if (iosize > 1024                 ) print $1\",\"$6 }' data/dump-simul.csv" using 1:2 with points lt rgb "#095000" pt 7 ps 0.5 title 'size > 1MiB'

# Bandwidth

set title "I/O access - bandwidth" 
set ylabel "KiB/s"
set ytics 1024

set term postscript eps color size 20cm,7cm
set output 'tmp/graph_throughput.eps'

plot "<awk 'BEGIN {FS=\",\"; timo=\"\"} {x=x+($7 * $8)/1024; if (timo != $1) {print $1\",\"x; timo = $1; x = 0} }' data/dump-simul.csv" with boxes title 'bandwidth (KiB/s)'

# IOPS

set title "I/O access - I/O operations per second" 
set ylabel "iops"
set ytics 5

set term postscript eps color size 20cm,7cm
set output 'tmp/graph_iops.eps'

plot "<awk 'BEGIN {FS=\",\"; timo=\"\"} {x=x+1; if (timo != $1) {print $1\",\"x; timo = $1; x = 0} }' data/dump-simul.csv" with boxes title 'I/O operations per second'
